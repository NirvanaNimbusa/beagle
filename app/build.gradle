apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

android {
    compileSdkVersion libraryCompileSdkVersion
    defaultConfig {
        applicationId "com.pandulapeter.beagleExample"
        minSdkVersion libraryMinimumSdkVersion
        targetSdkVersion libraryTargetSdkVersion
        versionCode libraryVersionCode
        versionName "0.0.1"
        buildConfigField("String", "BUILD_DATE", "\"${new Date().format("yyyy.MM.dd")}\"")
    }
    signingConfigs {
        internal {
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("internal.keystore")
            storePassword "android"
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.internal
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            applicationIdSuffix ".release"
            versionNameSuffix "-release"
            signingConfig signingConfigs.internal
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    flavorDimensions "beagle-ui"
    productFlavors {
        activity {
            applicationIdSuffix ".activity"
            versionNameSuffix "-activity"
        }
        bottomSheet {
            applicationIdSuffix ".bottomSheet"
            versionNameSuffix "-bottom-sheet"
        }
        dialog {
            applicationIdSuffix ".dialog"
            versionNameSuffix "-dialog"
        }
        drawer {
            applicationIdSuffix ".drawer"
            versionNameSuffix "-drawer"
        }
        view {
            applicationIdSuffix ".view"
            versionNameSuffix "-view"
        }
    }
}

configurations {
    activityDebugImplementation
    bottomSheetDebugImplementation
    dialogDebugImplementation
    drawerDebugImplementation
    viewDebugImplementation
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    activityDebugImplementation project(":ui-activity")
    bottomSheetDebugImplementation project(":ui-bottom-sheet")
    dialogDebugImplementation project(":ui-dialog")
    drawerDebugImplementation project(":ui-drawer")
    viewDebugImplementation project(":ui-view")
    releaseImplementation project(':noop')
}