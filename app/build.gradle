apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

android {
    compileSdkVersion libraryCompileSdkVersion
    defaultConfig {
        applicationId "com.pandulapeter.beagle"
        minSdkVersion libraryMinimumSdkVersion
        targetSdkVersion libraryTargetSdkVersion //TODO: Target SDK 30
        versionCode 9
        versionName "0.0.9"
        buildConfigField("String", "BUILD_DATE", "\"${new Date().format("yyyy-MM-dd")}\"")
        buildConfigField("String", "BEAGLE_VERSION", "\"$libraryVersion\"")
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    signingConfigs {
        internal {
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("internal.keystore")
            storePassword "android"
        }
        release {
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("internal.keystore")
            storePassword "android"
        }
    }
    buildFeatures {
        dataBinding = true
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.internal
        }
        internal {
            debuggable false
            minifyEnabled true
            shrinkResources true
            multiDexEnabled false
            applicationIdSuffix ".internal"
            versionNameSuffix "-internal"
            signingConfig signingConfigs.internal
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            matchingFallbacks = ["debug"]
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            multiDexEnabled false
            applicationIdSuffix ".release"
            versionNameSuffix "-release"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    flavorDimensions "beagle-ui"
    productFlavors {
        activity {
            applicationIdSuffix ".activity"
            versionNameSuffix "-activity"
        }
        bottomSheet {
            applicationIdSuffix ".bottomSheet"
            versionNameSuffix "-bottom-sheet"
        }
        dialog {
            applicationIdSuffix ".dialog"
            versionNameSuffix "-dialog"
        }
        drawer {
            applicationIdSuffix ".drawer"
            versionNameSuffix "-drawer"
        }
        view {
            applicationIdSuffix ".view"
            versionNameSuffix "-view"
        }
    }
}

configurations {
    activityDebugImplementation
    activityInternalImplementation
    bottomSheetDebugImplementation
    bottomSheetInternalImplementation
    dialogDebugImplementation
    dialogInternalImplementation
    drawerDebugImplementation
    drawerInternalImplementation
    viewDebugImplementation
    viewInternalImplementation
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "androidx.appcompat:appcompat:1.3.0-alpha01" //TODO: Use $appCompatVersion once 1.3.0 becomes stable
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "com.google.android.material:material:1.3.0-alpha02" //TODO: Use $materialVersion after 1.3.0 becomes stable
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "org.koin:koin-androidx-viewmodel:2.1.6"
    implementation("com.squareup.retrofit2:retrofit:$retrofitVersion") { exclude module: "okhttp" }
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "androidx.recyclerview:recyclerview:1.2.0-alpha05" //TODO: Use $recyclerViewVersion once 1.2.0 becomes stable
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    implementation "com.android.billingclient:billing:3.0.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    activityDebugImplementation project(":ui-activity")
    activityDebugImplementation project(":log")
    activityInternalImplementation project(":ui-activity")
    activityInternalImplementation project(":log")
    bottomSheetDebugImplementation project(":ui-bottom-sheet")
    bottomSheetDebugImplementation project(":log")
    bottomSheetInternalImplementation project(":ui-bottom-sheet")
    bottomSheetInternalImplementation project(":log")
    dialogDebugImplementation project(":ui-dialog")
    dialogDebugImplementation project(":log")
    dialogInternalImplementation project(":ui-dialog")
    dialogInternalImplementation project(":log")
    drawerDebugImplementation project(":ui-drawer")
    drawerDebugImplementation project(":log")
    drawerInternalImplementation project(":ui-drawer")
    drawerInternalImplementation project(":log")
    viewDebugImplementation project(":ui-view")
    viewDebugImplementation project(":log")
    viewInternalImplementation project(":ui-view")
    viewInternalImplementation project(":log")
    releaseImplementation project(":noop")
    releaseImplementation project(":log-noop")
}